{-# LANGUAGE FlexibleInstances #-}
-- {-# LANGUAGE MultiParamTypeClasses #-}
-- {-# LANGUAGE TypeSynonymInstances #-}
{-# LANGUAGE FunctionalDependencies #-}

module Concurrency.PoorMan where

import Control.Monad (ap, liftM)
import qualified System.IO as IO
import qualified Data.IORef as IO
import qualified Network.Socket as Socket

data Action
  = Atomic (IO Action) -- an atomic computation, returning a new action
  | Fork Action Action -- create a new thread
  | Stop -- terminate this thread

writeAction :: String -> Action
writeAction = \case
  "" -> Stop
  c : cs -> Atomic $ do
    putChar c
    pure $ writeAction cs

prog :: Action
prog = Fork (writeAction "Hello\n") (writeAction "CIS 5520\n")

scheduled :: [Action] -> IO ()
scheduled = \case
  [] -> pure ()
  action' : actions ->
    case action' of
      Atomic io -> do
        action <- io
        scheduled (actions <> [action])
      Fork action1 action2 -> scheduled (actions <> [action2, action1])
      Stop -> scheduled actions

writeComputation :: String -> Action -> Action
writeComputation input k = case input of
  "" -> k
  c : cs -> Atomic $ do
    putChar c
    pure (writeComputation cs k)

prog3 :: Action
prog3 = writeComputation "Hello" (writeComputation " CIS 5520\n" Stop)

sequenceComputation ::
  (Action -> Action) ->
  (Action -> Action) ->
  (Action -> Action)
sequenceComputation = (.)

hello5520Computation :: Action -> Action
hello5520Computation =
  writeComputation "Hello" `sequenceComputation` writeComputation " CIS5520\n"

-- >>> scheduled [ hello5520Computation Stop ]
-- >>> scheduled [ Fork (hello5520Computation Stop) (hello5520Computation Stop) ]
-- >>> let bomb = writeComputation "bomb" bomb
-- scheduled [ bomb ]

readComputation :: (Char -> Action) -> Action
readComputation f = Atomic $ f <$> getChar

sequenceComp ::
  ((a -> Action) -> Action) -> -- last action takes an argument
  (a -> (b -> Action) -> Action) -> -- pass to another
  (b -> Action) ->
  Action
sequenceComp f g h = f (`g` h)

type CM a = (a -> Action) -> Action

sequenceCompM :: CM a -> (a -> CM b) -> CM b
sequenceCompM f g k = f (`g` k)

returnCompM :: a -> CM a
returnCompM x k = k x

newtype C a = MkC {runC :: (a -> Action) -> Action}

instance Functor C where
  fmap f m = MkC (\k -> runC m (k . f))

instance Applicative C where
  pure x = MkC (\k -> k x)
  (<*>) = ap

instance Monad C where
  return = pure
  m >>= f = MkC (\k -> runC m (\x -> runC (f x) k))

atomic :: IO a -> C a
atomic io = MkC (\k -> Atomic $ k <$> io)

-- Create a fork action with the given computation (stopping on completion)
-- and the the current continuation.
fork :: C () -> C ()
fork m = MkC (\k -> Fork (runC m (const Stop)) (k ()))

run :: C a -> IO ()
run m = scheduled [runC m (const Stop)]

class (Monad m) => OutputMonad m where
  write :: String -> m ()

instance OutputMonad IO where
  write :: String -> IO ()
  write = putStr

instance OutputMonad C where
  write :: String -> C ()
  -- write = atomic . write
  write "" = atomic (write "")
  write (c : cs) = atomic (write [c]) >> write cs

infinite :: (OutputMonad m) => String -> m ()
infinite = (>>) <$> write <*> infinite

-- infinite msg = write msg >> infinite msg

example :: C ()
example = do
  write "It's raining..."
  fork (infinite "dog\n")
  infinite "cat\n"

class (Monad m) => InputMonad m where
  input :: m (Maybe String)

instance InputMonad IO where
  input :: IO (Maybe String)
  input = do
    ready <- IO.hReady IO.stdin
    if ready
      then Just <$> getLine
      else pure Nothing

instance InputMonad C where
  input :: C (Maybe String)
  input = atomic input

ioLoop :: (InputMonad m, OutputMonad m) => String -> m String
ioLoop s = do
  input >>= \case
    Just x -> pure $ "Thread " <> s <> ": " <> x
    Nothing -> do
      write s
      ioLoop s

example2 :: C ()
example2 = do
  fork $ ioLoop "a" >>= write
  ioLoop "b" >>= write

data Msg
  = Add
  | Reset
  | Print
  | Quit

class (Monad m) => MsgMonad b m | m -> b where
  newMailBox :: m b
  sendMsg :: b -> Msg -> m ()
  checkMsg :: b -> m (Maybe Msg)

type MailBox = IO.IORef (Maybe Msg)

instance MsgMonad MailBox IO where
  newMailBox :: IO MailBox
  newMailBox = IO.newIORef Nothing

  sendMsg :: MailBox -> Msg -> IO ()
  sendMsg v a = IO.writeIORef v (Just a)

  checkMsg :: MailBox -> IO (Maybe Msg)
  checkMsg v =
    IO.readIORef v >>= \case
      Just y -> IO.writeIORef v Nothing >> pure (Just y)
      Nothing -> pure Nothing

instance MsgMonad MailBox C where
  newMailBox :: C MailBox
  newMailBox = atomic newMailBox

  sendMsg :: MailBox -> Msg -> C ()
  sendMsg v msg = atomic (sendMsg v msg)

  checkMsg :: MailBox -> C (Maybe Msg)
  checkMsg v = atomic (checkMsg v)

simulation :: MailBox -> Integer -> C ()
simulation mailBox integer =
    checkMsg mailBox >>= \case
        Just Add -> do
            write "Adding...\n"
            simulation mailBox (integer + 1)
        Just Reset -> do
            write "Resetting...\n"
            simulation mailBox 0
        Just Print -> do
            write ("Current value is " <> show integer <> "\n")
            simulation mailBox integer
        Just Quit -> do
            write "Done\n"
        Nothing -> simulation mailBox integer

interface :: MailBox -> C (Maybe String) -> C ()
interface mailBox getInput = loop where
    loop = getInput >>= \case
        Just "a" -> sendMsg mailBox Add >> loop
        Just "r" -> sendMsg mailBox Reset >> loop
        Just "p" -> sendMsg mailBox Print >> loop
        Just "q" -> sendMsg mailBox Quit
        Just s -> write ("Unknown command: " <> s <> "\n") >> loop
        Nothing -> loop

example6 :: C ()
example6 = do
    mailBox <- newMailBox
    fork $ simulation mailBox 0
    interface mailBox input

